plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.researchgate.release' version '2.4.0'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

ext {
    dropwizardVersion = '1.0.0'
    junitVersion = '4.12'
}

group = 'co.wrisk.dropwizard'

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }

    maven {
        url "s3://repo.wrisk.co/release"
        credentials(AwsCredentials) {
            accessKey "${AWS_ACCESS_KEY_ID}"
            secretKey "${AWS_SECRET_ACCESS_KEY}"
        }
    }
}


compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.deprecation = true
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.deprecation = true
}
dependencies {
    compileOnly "io.dropwizard:dropwizard-logging:$dropwizardVersion"
    compile "co.wrisk.logback:logback-ext-cloudwatch-appender:1.0.3"

    testCompile "junit:junit:${junitVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { !version.contains("SNAPSHOT") }
    sign configurations.archives
}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                configuration.resolve()
            }
            subProject.configurations.each { configuration ->
                configuration.resolve()
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives

uploadArchives {

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Dropwizard component to enable cloudwatch log appender'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Dropwizard component to enable cloudwatch log appender'
                url 'https://github.com/WriskHQ/dropwizard-logging-cloudwatch'

                scm {
                    connection 'scm:git:git@github.com:WriskHQ/dropwizard-logging-cloudwatch.git'
                    developerConnection 'scm:git:git@github.com:WriskHQ/dropwizard-logging-cloudwatch.git'
                    url 'https://github.com/WriskHQ/dropwizard-logging-cloudwatch'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://raw.githubusercontent.com/WriskHQ/dropwizard-logging-cloudwatch/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'aharin'
                        name 'Alex Harin'
                        email 'alex@wrisk.co'
                    }
                }
            }
        }
    }
}





