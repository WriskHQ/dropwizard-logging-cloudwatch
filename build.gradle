plugins {
    id 'java'
    id: 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

ext {
    dropwizardVersion = '1.0.0'
    junitVersion = '4.12'
}

group = 'co.wrisk.dropwizard'

def buildNumber = "${System.env.CI_BUILD_NUMBER ? System.env.CI_BUILD_NUMBER : 'manual'}"
def semanticVersion = "1.0.0"
version = "$semanticVersion.$buildNumber"

repositories {
    jcenter()

    maven {
        url "s3://repo.wrisk.co/release"
        credentials(AwsCredentials) {
            accessKey "${AWS_ACCESS_KEY_ID}"
            secretKey "${AWS_SECRET_ACCESS_KEY}"
        }
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.deprecation = true
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.deprecation = true
}
dependencies {
    compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-logging:$dropwizardVersion"
    compile "co.wrisk.logback:logback-ext-cloudwatch-appender:1.0.0.6"

    testCompile "junit:junit:${junitVersion}"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                configuration.resolve()
            }
            subProject.configurations.each { configuration ->
                configuration.resolve()
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}
publishing {
    repositories {
        maven {
            url "s3://repo.wrisk.co/release"
            credentials(AwsCredentials) {
                accessKey "${AWS_ACCESS_KEY_ID}"
                secretKey "${AWS_SECRET_ACCESS_KEY}"
            }
        }
    }
}



